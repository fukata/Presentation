#!/usr/bin/env perl
use strict;
use warnings;
use utf8;
use autodie;

use Cwd;
use FindBin;
use File::Copy;
use Text::Markdown 'markdown';
use Template;

open(my $datafile, "<", "main.txt"); 

my $root = $FindBin::RealBin;
my $export = Cwd::getcwd();
my $delimiter = ">-----";
my $html = '';
my $title = undef;
my $theme = $ARGV[0] || 'default';

sub to_html {
	my $buf = shift;
	my $h = markdown($buf);
	return "\n<div class=\"contents\">\n$h</div>";
}

sub load_index {
	my ($theme, $title, $html) = @_;
	my $tt = Template->new({
		INCLUDE_PATH => "$root/themes/",
	});
	my $result = '';
	$tt->process("$theme/index.tt", {title=>$title, html=>$html}, \$result);
	return $result;
}

my $buf = undef;
while (my $line = <$datafile>){
	chomp($line);
	unless ($title) {
		$title = $line;
		next;
	}

	if ($line eq $delimiter) {
		$html .= to_html($buf);
		$buf = undef;
	} else {
		$buf .= "\n$line";
	}
}
$html .= to_html($buf) if $buf;
close($datafile);

# index.html
open(my $out, ">", "$export/index.html");
print $out load_index($theme, $title, $html);
close($out);

# css and js
copy("$root/themes/$theme/main.css", "$export/main.css");
copy("$root/themes/$theme/jquery.js", "$export/jquery.js");
copy("$root/themes/$theme/main.js", "$export/main.js");
