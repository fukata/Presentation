#!/usr/bin/env perl
use strict;
use warnings;
use utf8;
use autodie;

use Data::Dumper;
use Cwd;
use FindBin;
use File::Copy::Recursive qw/rcopy/;
use Text::Markdown 'markdown';
use Template;
use JSON::XS;

open(my $datafile, "<", "main.txt"); 

my $root = $FindBin::RealBin;
my $export = Cwd::getcwd();
my $config = {
	delimiter => ">-----",
	theme => "default",
	contents_class => "contents",
};
my $html = '';
my $title = undef;
$config->{theme} = $ARGV[0] if $ARGV[0];

sub load_config {
	my $user_config = undef;
	my $theme_config = undef;
	if (-f "$export/config.json") {
		open(my $configfile, "<", "$export/config.json"); 
		local $/ = undef;
		my $user_config = JSON::XS->new->utf8->decode(<$configfile>);
		$config = {%$config, %$user_config};
		close($configfile);
	}

	if (-f "$root/themes/$config->{theme}/config.json") {
		open(my $configfile, "<", "$root/themes/$config->{theme}/config.json"); 
		local $/ = undef;
		my $theme_config = JSON::XS->new->utf8->decode(<$configfile>);
		$config = {%$config, %$theme_config};
		close($configfile);
	}
	
	$config = {%$config, %$user_config} if $user_config;
}

sub to_html {
	my $buf = shift;
	my $h = markdown($buf);
	return "\n<div class=\"$config->{contents_class}\">\n$h</div>";
}

sub load_index {
	my ($theme, $title, $html) = @_;
	my $tt = Template->new({
		INCLUDE_PATH => "$root/themes/",
	});
	my $result = '';
	$tt->process("$theme/index.tt", {title=>$title, html=>$html, config=>$config}, \$result);
	return $result;
}

load_config();

my $buf = undef;
while (my $line = <$datafile>){
	chomp($line);
	unless ($title) {
		$title = $line;
		next;
	}

	if ($line eq $config->{delimiter}) {
		next unless $buf;
		$html .= to_html($buf);
		$buf = undef;
	} else {
		$buf .= "\n$line";
	}
}
$html .= to_html($buf) if $buf;
close($datafile);

# index.html
open(my $out, ">", "$export/index.html");
print $out load_index($config->{theme}, $title, $html);
close($out);

# css and js
rcopy("$root/assets", "$export/assets");
rcopy("$root/themes/$config->{theme}/assets", "$export/assets");

